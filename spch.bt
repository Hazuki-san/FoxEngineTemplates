// SPCH - TppSpeechData
// By RLC - inspired by other FOX templates by the researchers of Modders' Heaven!
// https://metalgearmodding.fandom.com/wiki/SPCH
#include "common/common.bt"
LittleEndian();

struct Header
{
    char signature[4] <hidden=true>; // "spch"
    ushort endianness;               // PS3 files have a 01 here.
    if (endianness != 0)
    {
        BigEndian();
    }
    ushort labelCount;
    FSkip(8 * header.labelCount);    // 8 bytes of padding per label entry. Was it ever used? Who knows!
} header <bgcolor = 0x5B507A>;         // coolor

struct SpeechSequence
{
    uint labelName;  // StrCode32 hash of the label's name, used by .lua and .frt.
    uint dialogueEvent;      // FNV132, Dynamic Dialogue event name 
    uint speechPartCount; // Number of voice clip entries in this label.
    struct SpeechPart
    {
        uint characterName;     // StrCode32, speaker's voice type.
        uint condition;      // FNV132, "condition" state group's state name for the sound 
        uint animationAct;   // StrCode32 hashes of an animation to do.
        FSkip(4);
        float afterPauseSeconds;  // [says line] [pause this long before next line is said]
			
    } speechParts[speechPartCount] <optimize=false>;
} speechSequence[header.labelCount] <optimize=false,bgcolor = 0x5B618A>;